apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

dependencies {
    // Testing
    testCompile "junit:junit:4.12"
}

group= "com.mod-buildcraft"
archivesBaseName = "buildcraft-libexpression"

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir "src/autogen/java"
        }
        resources {
            srcDir "src/main/resources"
        }
    }
    test {
        java {
            srcDir "src/test/java"
        }
    }
    generator {
        java {
            srcDir "src/generator/java"
        }
        resources {
            srcDir "src/generator/resources"
        }
    }
}

task srcJar (type:Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

build.dependsOn(srcJar)

task removeOldAutoGen {
    doLast {
        delete "src/autogen/java/buildcraft"
    }
}

task generateSources(type: JavaExec, dependsOn: removeOldAutoGen) {
    classpath sourceSets.generator.runtimeClasspath
    main = "buildcraft.meta.generate.AutoGenerator"
    args '-out', 'src/autogen/java', '-run', '-quiet'
}

eclipseClasspath.dependsOn generateSources
ideaModule.dependsOn generateSources
compileJava.dependsOn generateSources

publishing {
    repositories {
        maven {
            url System.getenv("MAVEN_DIR") ?: "build/maven"
        }
    }
    publications {
        pub_main(MavenPublication) {
            groupId "com.mod-buildcraft"
            artifactId "buildcraft-libexpression"
            version project.version

            artifact jar
        }
        pub_mainSrcJar(MavenPublication) {
            groupId "com.mod-buildcraft"
            artifactId "buildcraft-libexpression"
            version project.version

            artifact srcJar
        }
    }
}


