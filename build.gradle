buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
		maven { url 'https://maven.fabricmc.net' }
		maven { url 'https://repo.sleeping.town' }
	}
	dependencies {
		classpath 'com.unascribed:voldeloom:1.0+1.4.7'
	}
}

apply plugin: 'java'
apply plugin: 'com.unascribed.voldeloom'

sourceCompatibility = 6
targetCompatibility = 6

def forgeVersion = '1.4.7-6.6.2.534'

def mcp = "mcp726a.zip";
def forgeZip = "forge${forgeVersion}.zip"
def mcpForged = "mcp726a-forged.zip"

if (!file(mcp).exists()) {
	println "Downloading ${mcp} from archive.org..."
	file(mcp).withOutputStream {
		it << new URL("https://archive.org/download/minecraftcoderpack/minecraftcoderpack.zip/minecraftcoderpack%2F${project.minecraft_version}%2F${mcp}").openStream()
	}
}
if (!file(forgeZip).exists()) {
	println "Downloading Forge ${forgeVersion} source..."
	file(forgeZip).withOutputStream {
		it << new URL("https://maven.minecraftforge.net/net/minecraftforge/forge/${forgeVersion}/forge-${forgeVersion}-src.zip").openStream()
	}
}
if (!file(mcpForged).exists()) {
	println "Merging Forge config into MCP..."
	'./merge-forge.sh'.execute()
}

archivesBaseName = "buildcraft-A-1.4.7"
def buildNumber = '18'
version = "3.4.5"
group = "com.mod-buildcraft"

repositories {
	maven { url 'https://maven.fabricmc.net/' }
	maven { url 'https://maven.minecraftforge.net/' }
	maven { url 'https://repo.spongepowered.org/maven' }
}

sourceSets {
	main {
		java.srcDirs = ["build/filteredSrc"]
		resources.srcDirs = ["buildcraft_resources", "buildcraft_localization"]
	}
}

task remapSrc(type: Copy) {
	inputs.property 'version', project.version
	inputs.property 'buildNumber', buildNumber

	from "common"

	filesMatching('buildcraft/core/Version.java') {
		expand 'VERSION': project.version, 'BUILD_NUMBER': buildNumber
	}

	into "build/filteredSrc"
}

compileJava.dependsOn remapSrc

dependencies {
	minecraft "com.mojang:minecraft:1.4.7"
	forge "net.minecraftforge:forge:${forgeVersion}:universal@zip"
	implementation "com.google.guava:guava:12.0.1"
	mappings files(mcpForged)
}

processResources {
	inputs.property "version", project.version
	inputs.property 'buildNumber', buildNumber

	exclude 'README.md'

	filesMatching("mcmod.info") {
		expand "version": project.version+"."+buildNumber
	}

	filesMatching("build.number") {
		expand "buildNumber": buildNumber
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

